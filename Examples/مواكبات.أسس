اشمل "مـتم/نـص"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/بـعدم"؛
اشمل "مـتم/لـا_مضمون"؛
اشمل "مـتم/أخطاء"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/بـوستغرس.أسس" })؛
استخدم مـتم؛
استخدم صـفوف؛

عرف قب: قـاعدة_بيانات(مـشغل_بوستغرس(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "alusus"؛
    اسم_المستخدم = "alusus"؛
    كلمة_السر = "alusus"؛
    عنوان_الخادم = "0.0.0.0"؛
    المنفذ = 5432؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1، نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

دالة أعد_الضبط() {
    قب.نفذ("drop table if exists table_versions");
    قب.نفذ("drop table if exists vehicles");
    قب.نفذ("drop table if exists prices");
}

دالة أدرج_بيانات(سكويل: مـؤشر_محارف) {
    عرف نتيجة: لـا_مضمون[مـصفوفة[مـصفوفة[بـعدم[نـص]]]] = قب.نفذ_جلبا(سكويل)؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < نتيجة.هات_الطول()، ++ع {
        عرف م: صـحيح؛
        لكل م = 0، م < نتيجة(ع).هات_الطول()، ++م {
            طـرفية.اطبع(نتيجة(ع)(م))؛
            طـرفية.اطبع("\t\t")؛
        }
        طـرفية.اطبع("\ج")؛
    }
}

أعد_الضبط()؛

طـرفية.اطبع("\ج\ج <<< اختبار 1 >>>\ج\ج")؛

وحدة اخـتبار1 {
    @جدول["vehicles"، 1]
    صنف سـيارة {
        عرف_أساسيات_الجدول[]؛

        @إلزامي
        @فهرس_رئيسي
        @عـدد_صحيح
        @حقل["id"]
        عرف المعرف: صحيح؛

        @مـحارف_مرنة["50"]
        @مبدئي["سيارتي"]
        @حقل["name"]
        عرف الاسم: نـص؛

        @عـدد_عائم
        @حقل["price"]
        عرف السعر: عائم؛
    }
}

عرف هيكل1: اخـتبار1.سـيارة؛
قب.مهيكل[هيكل1].واكب().{
    إذا ليس هذا.أهو_عدم() {
        نـظام.فشل(1، هذا.هات_الرسالة())؛
    }
}؛

عرف ع: صـحيح؛
لكل ع = 1، ع < 5، ++ع {
    عرف س: اخـتبار1.سـيارة؛
    س.المعرف = ع؛
    س.الاسم = نـص("سيارة ") + ع؛
    س.السعر = 200.0 * ع؛
    قب.احفظ[اخـتبار1.سـيارة](س)؛
}

أدرج_بيانات("select * from table_versions")؛
أدرج_بيانات("select * from vehicles")؛

طـرفية.اطبع("\ج\ج <<< اختبار 2 >>>\ج\ج")؛

وحدة اخـتبار2 {
    @جدول["vehicles"، 2]
    صنف سـيارة {
        عرف_أساسيات_الجدول[]؛

        @إلزامي
        @فهرس_رئيسي
        @عـدد_صحيح
        @حقل["id"]
        عرف المعرف: صحيح؛

        @مـحارف_مرنة["50"]
        @مبدئي["سيارتي"]
        @حقل["name"]
        عرف الاسم: نـص؛

        @عـدد_عائم
        @حقل["price"]
        عرف السعر: عائم؛

        @عـدد_صحيح
        @حقل["year"]
        عرف السنة: صـحيح؛

        @مواكبة[1, 2]
        دالة حدث_1_إلى_2(قب: سند[قـاعدة_بيانات]): سـندنا[خـطأ] {
            أرجع قب.نفذ("alter table vehicles add column year integer").الخطأ؛
        }
    }
}

عرف هيكل2: اخـتبار2.سـيارة؛
قب.مهيكل[هيكل2].واكب().{
    إذا ليس هذا.أهو_عدم() {
        نـظام.فشل(1، هذا.هات_الرسالة())؛
    }
}؛

قب.من[اخـتبار2.سـيارة].حيثما[المعرف == 2].حدث[السنة = 2012];

أدرج_بيانات("select * from table_versions")؛
أدرج_بيانات("select * from vehicles")؛
